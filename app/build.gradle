apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {
    namespace 'com.facebook.encapp'
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.facebook.encapp"
        minSdkVersion 27
        targetSdkVersion 31
        versionCode 1
        versionName "1.23"
        setProperty("archivesBaseName", applicationId + "-v" + versionName)

        ndk {
            abiFilters "arm64-v8a"
        }
    }

    packagingOptions {
        pickFirst 'lib/x86/libc++_shared.so'
        pickFirst 'lib/x86_64/libc++_shared.so'
        pickFirst 'lib/armeabi-v7a/libc++_shared.so'
        pickFirst 'lib/arm64-v8a/libc++_shared.so'
        pickFirst 'META-INF/Notice.txt'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        //If you want to continue even if errors found use following line
        abortOnError false
    }

    sourceSets {
        main {
            proto {
                srcDir '../proto/'
            }

            jniLibs.srcDirs = ['src/main/jniLibs', '../lcevc/x264/libs']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    ndkVersion '21.3.6528147'

    flavorDimensions = ['layout']

    productFlavors {
        aosp {
            dimension 'layout'
        }

        full {
            dimension 'layout'
        }

        fullNoBenchmark {
            dimension 'layout'
        }

        demo {
            dimension 'layout'
        }

        nonconan {
            dimension 'layout'
        }
    }
}

configurations {
    javacpp
}

task javacppExtract(type: Copy) {
    dependsOn configurations.javacpp

    from { configurations.javacpp.collect { zipTree(it) } }
    include "lib/**"
    into "$buildDir/javacpp/"
    android.sourceSets.main.jniLibs.srcDirs += ["$buildDir/javacpp/lib/"]

    tasks.getByName('preBuild').dependsOn javacppExtract
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    api 'androidx.appcompat:appcompat:1.0.0'
    implementation group: 'com.google.protobuf', name: 'protobuf-java', version: '3.24.0'
    implementation group: 'com.google.protobuf', name: 'protobuf-java-util', version: '3.24.0'
    implementation 'com.google.code.gson:gson:2.8.0'

    implementation 'com.vnova.lcevc:eil:3.13.2'
    implementation 'com.vnova.lcevc.eilp:mediacodec:3.13.2'
    implementation 'com.vnova.lcevc.eilp:x264:3.13.2'
    implementation 'com.vnova.lcevc:jni:3.13.2'
    implementation 'com.vnova.lcevc:util:3.13.2'

    implementation group: 'org.bytedeco', name: 'javacv', version: '1.5.11'
    javacpp group: 'org.bytedeco', name: 'ffmpeg-platform', version: '7.1-1.5.11'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.24.0'
    }
    plugins {
        java {}
    }

    generateProtoTasks {
        all().configureEach { task ->
            task.plugins {
                java {}
            }
        }
    }
}